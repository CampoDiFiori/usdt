// A definition of the basic grammar of DTrace provider definitions.
// Copyright 2021 Oxide Computer Company

// Some basic tokens
PROBE_KEY = @{ "probe" }
PROVIDER_KEY = @{ "provider" }
SEMICOLON = @{ ";" }
LEFT_PAREN = @{ "(" }
RIGHT_PAREN = @{ ")" }
LEFT_BRACE = @{ "{" }
RIGHT_BRACE = @{ "}" }

// A valid identifier for a provider or probe
IDENTIFIER = @{ ASCII_ALPHA+ ~ (ASCII_ALPHANUMERIC | "_")* }

// Data types
BIT_WIDTH = @{ "8" | "16" | "32" | "64" }
SIGNED_INT = ${ "int" ~ BIT_WIDTH ~ "_t" }
UNSIGNED_INT = ${ "uint" ~ BIT_WIDTH ~ "_t" }
FLOAT = @{ "float" }
DOUBLE = @{ "double" }
STRING = @{ "string" }
DATA_TYPE = { STRING | UNSIGNED_INT | SIGNED_INT | FLOAT | DOUBLE}

// Argument and argument lists
ARGUMENT = {
	DATA_TYPE
	~ ("," ~ DATA_TYPE)*
}
ARGUMENT_LIST = {
	LEFT_PAREN
	~ ARGUMENT*
	~ RIGHT_PAREN
}

// Definition of a probe
PROBE = {
	PROBE_KEY
	~ IDENTIFIER
	~ ARGUMENT_LIST
	~ SEMICOLON
}

// Definition of a provider
PROVIDER = {
	PROVIDER_KEY
    ~ IDENTIFIER
    ~ LEFT_BRACE
    ~ (PROBE)+
    ~ RIGHT_BRACE
    ~ SEMICOLON
}

PRAGMA = ${
	"#pragma"
	~ SPACE+
	~ (!("\n") ~ ANY)*
	~ SPACE*
	~ "\n"
}

SPACE = _{ " " | "\t" }

// Files consist of providers and pragmas
FILE = {
	SOI
	~(
		PROVIDER
		| PRAGMA
	)*
	~EOI
}

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }